plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    implementation project(':graphql-mock-matchers-core')
    compileOnly libs.wiremock.core

    testImplementation libs.wiremock.core
    testImplementation platform(libs.junit.bom)
    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.launcher
}

tasks.named('test', Test) {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'graphql-mock-matchers-wiremock'

            pom {
                name = 'graphql-mock-matchers-wiremock'
                description = 'WireMock extension for semantic GraphQL request matching'
                url = 'https://github.com/minerasoft/graphql-mock-matchers'

                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                developers {
                    developer {
                        id = 'ram'
                        name = 'Ram'
                    }
                }

                scm {
                    url = 'https://github.com/minerasoft/graphql-mock-matchers'
                    connection = 'scm:git:https://github.com/minerasoft/graphql-mock-matchers.git'
                    developerConnection = 'scm:git:ssh://git@github.com:minerasoft/graphql-mock-matchers.git'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'OSSRH'
            url = uri(version.toString().endsWith('SNAPSHOT')
                    ? 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                    : 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/')
            credentials {
                username = findProperty('ossrhUsername') ?: System.getenv('OSSRH_USERNAME')
                password = findProperty('ossrhPassword') ?: System.getenv('OSSRH_PASSWORD')
            }
        }
    }
}

signing {
    def signingKey = findProperty('signingKey') ?: System.getenv('SIGNING_KEY')
    def signingPassword = findProperty('signingPassword') ?: System.getenv('SIGNING_PASSWORD')
    if (signingKey && signingPassword) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenJava
    }
}
